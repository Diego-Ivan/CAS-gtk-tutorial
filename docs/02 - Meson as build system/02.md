# Managing namespaces in Vala and using subdirectories in Meson

Now, we have our GtkApplication, so let's create a window by also extending the class. In this case, a GtkWindow will not be enough for us, we need a subclass of it: `GtkApplicationWindow`. This subclass takes a single parameter for construction:

```
(Gtk.Application application)
```

You may be wondering why are we not using the *default* GtkWindow, and the reason is, that this subclass offers some cool integration with Gtk.Application.Let's start writing some code. First, let's extend our GtkApplicationWindow class in a new file named `window.vala` and let's add it to Meson too.

```meson
project_files = [
	'main.vala',
	'Application.vala',
	'window.valaÂ´
]
```

And, let's write our first lines of code in our file:

```vala
public class MyWindow : Gtk.ApplicationWindow {

}
```

Now, let's add the constructor. As you remember, it needs a single parameter: a Gtk.Application, so let's add that to the constructor:

```vala
public MyWindow (Gtk.Application app) {
    
}
```

As you may remember, to write the construct-only properties of our Object, we use the `Object` method, and write the property name with the value at its right:

```vala
Object (application: app);
```

And now, let's add some stuff to our window. You may remember that we used a `window.method (value)` to create or add stuff for our window. In this case, we can use the `this` keyword in our object, something like this:

```vala
this.set_title ("My Window");
this.set_default_size (400, 600);
```

But this would cause that our code was plagued with `this` `this` `this` `this` in every single place. Fortunately, Vala has a feature in which, when you are writing something within a class, it assumes that the methods you'll be refering to are inside that class, the same happens with properties. This is why you can remove the `this` keyword for this methods. Our code will look cleaner, something like this:

```vala
set_title ("My Window");
set_default_size (400, 600);
```

Now, let's add some widgets to our window. While you *can* declare them within the constructor like this:

```vala
var label = new Gtk.Label ("My Label");
```

You are very likely need those properties in other methods, and you **must not** be constantly passing them from one method to other, this would make your code ugly and less understandable. We will declare them as properties for the whole class. Above where our constructor is defined we will write:

```vala
private Gtk.Label label;
```

And then, we would create the object within the constructor:

```vala
label = new Gtk.Label ("My Label");
```

Now, add it to the window with:

```vala
set_child (label);
```

And show the widgets with:

```vala
show_all ();
```

The next step that should be followed is, well, showing our window on screen. We need to put in our application file in order to have it shown. We will do this in our `activate` method:

First, let's define it above of the constructor:

```vala
private MyWindow main_window;
```

And now, let's declare the Window in our constructor. But wait, the `MyWindow` class requires a GtkApplication as parameter for construction... How do we do that? Well, we need to pass `this`, which will be the whole object, to our Window instance:

```vala
main_window = new MyWindow (this);
```

We also need to present it! so let's add:

```vala
main_window.present ();
```

This is correct, although it is recommended to verify if the instance of the window is `null`, and if it is, create a new instace. Now, it will present the window (if it was not `null` it would just go to run `present ()`

```vala
if (main_window == null) {
	main_window = new MyWindow (this);
}
main_window.present ();
```

As you can see, we verify that our `main_window` is not `null`, if it was, it would create a new instance, and then present it. We can improve the quality of this code. Vala does not require you to use brackets when after an if you have a single statement, so you can reduce the code to:

```vala
if (main_window == null)
	main_window = new MyWindow (this);

main_window.present ();
```

## Namespacing

You now may be wondering why our class is created like `new MyWindow ()` instead of the Gtk style that we were used to in the introduction of having a namespace like `Gtk.Window ()` or `Namespace.Class ()`. Well, let's implement namespaces in our class.

We can implement namespaces in Vala in two ways:

1. By stating the namespace in the same class declaration, like: `public class NamespaceName.ClassName : ParentClass ()`
2. By using a statement in curly brackets surrounding the class declaration, like:

```vala
namespace NamespaceName {
	public class ClassName : ParentClass () {

	}
}
```

We will use the second method, but you can use the first one if you are not quite liking the double indentation.

So, let's implement the namespaces in our app. I'll give my namespace the name of `GtkTutorial`, but you can use the one you like or prefer. It is recommended you use your application's name.

Also, in my case I wrote `MyApplication` and `MyWindow`. Now that we are using namespaces it will no longer be necessary to use this, so I'll remove them and write something like: `GtkTutorial.Application` and `GtkTutorial.Window`.

So, our files, now with namespaces, would be something like:

* Application.vala:

```vala
namespace GtkTutorial {
    public class Application : Gtk.Application {
        private Window main_window;
        public Application () {
            Object (
                application_id: "io.github.diegoivanme.gtk-tutorial",
                flags: ApplicationFlags.FLAGS_NONE
            );
        }

        protected override void activate () {
            print ("Hello World");
            if (main_window == null)
                main_window = new Window (this);

            main_window.present ();
        }
    }
}
```

* Window.vala

```vala
namespace GtkTutorial {
    public class Window : Gtk.ApplicationWindow {
        private Gtk.Label label;

        public Window (Gtk.Application app) {
            Object (application: app);
            label = new Gtk.Label ("My Label");

            set_default_size (400, 600);
            set_title ("My Window");

            set_child (label);
            show_all ();
        }
    }
}
```

* main.vala

```vala
namespace GtkTutorial {
    public static int main (string[] args) {
        var app = new Application ();
        return app.run (args);
    }
}
```

## Using subdirectories in Meson

Now that we will be using multiple files, as our project scales, we are required to use various directories in order to keep it as clean as possible. Meson supports subdirectories in two main ways. 

First, let's create subdirectory named `src`, that will contain the code files of our project (in this case, our `.vala` files). This might seem a little bit weird, as our project *rarely* uses any files, but this will help us later as we learn more ;D. Now, we should move `.vala` files over there.

Now that you have moved them, try running `ninja`, and it will clearly not find the files because they have been moved. 

Let's learn the first one, which is the simplest, but as your project grows it becomes a pain in the ass. This one is based on just write files as: `subdir/file`, which is *fine* if you have just a few files, but it's not scalable. Still, I recommend you to try it and see that in fact, **it works**

Now, the scalable and most used way:

First, we will learn another meson method: `subdir ('subdir name')`. This method, will look for another `meson.build` file in that subdirectory. It's basically splitting our `meson.build` file into various to make them more readable, and less painful. So, create a meson file in `src`, and let's move our file array over there, as that' where files are. Our `src/meson.build` would look something like this:

```meson
project_files = [
	'main.vala',
	'Application.vala',
	'window.vala'
]
```

And, let's add the `subdir` line to our `meson.build`. It should be put before the `executable` line. 

Your `meson.build` file will look something like this:

```meson
project ('gtk-tutorial', ['c', 'vala'])

project_deps = [
	dependency ('gio-2.0', version: '>=2.50'),
	dependency ('gtk4', version: '>=4.2.1')
]

subdir ('src')

executable (meson.project_name (), project_files,
	    dependencies: project_deps,
	    install: true
)
```

You might think this is enough... but, if you run `ninja`, it will tell you it does not find those files.

This has an explanation, of course. In the end, the `project_files`, is just an array of strings, that `meson` will start to look for in the place we tell it do it. The method that tells meson to look for them, is `executable`, that is in `meson.build`, and it will **not** find them in that folder. That's why we should move the `executable` method declaration to `src/meson.build` after we declare `project_files`.

Our `src/meson.build` will look like this:

```meson
project_files = [
	'main.vala',
	'Application.vala',
	'window.vala'
]

executable (meson.project_name (), project_files,
	    dependencies: project_deps,
	    install: true
)
```

And... before compiling, I recommend you to write:

```vala
message ("New subdirectories in Meson!");
```

In your `src/window.vala`, specifically in the Window constructor. You'll see why I'm asking for this in a moment.

Now, let's run `ninja` and run our program too, that you usually executed with:

```sh
./[project_name]
```

And, you are very likely notice that the message you put in `src/window.vala` is not there. This is because we are **not** executing the correct binary. In the subdirectory you put `executable`, it will put the binary, in this case, the `src/` directory will also be in `builddir/`, and we will execute our program like this:

```sh
./src/[project_name]
```

And, now, it should show the message just fine!