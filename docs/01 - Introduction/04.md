# Entries and spin buttons

So, by now, we will continue our tutorial by learning how to use some other very important and commonly used widgets, today: Entries and Spin Buttons!

Entries are used when we want to add some information and write in a blank space. By now, we will delete pretty much anything in our window, and we will replace it with the stuff we need.

We'll leave our file just like this:

```vala
public static int main (string[] args) {
    var app = new Gtk.Application (
        "io.github.diegoivanme.gtk-tutorial",
        ApplicationFlags.FLAGS_NONE
    );

    app.activate.connect (() => {
        var window = new Gtk.ApplicationWindow (app);
        window.set_title ("Hello World!");

        window.present ();
    });

    return app.run (args);
}
```

As you can see, we pretty much left our window in bare bones, but we will do something else. We will also learn how to use GtkBoxes.

We can create boxes like this:

```vala
var box = new Gtk.Box (Gtk.Orientation orientation, spacing)
```

So, we will add the following things to our code

```vala
var box = new Gtk.Box (Gtk.Orientation.VERTICAL, 5);
```

If you are wondering what is `Gtk.Orientation`, it is an enumerator with the possible values that the orientation can be, in this case, it will be vertical, but you can also choose horizontal if you are feeling curious about how it may look

So, now, we will create an entry, and a button. The text that is put on the entry, will be shown in the window title

Let's create our widgets:

```vala
var button = new Gtk.Button ();
var entry = new Gtk.Entry ();
var label = new Gtk.Label ("Set window title!");
```

Now, let's learn how to pack them in our Box. The methods `append` (which adds your widgets next to the previous one, in the n+1 position) and `prepend` (which adds your widget before all the others). Have this values:

```
(Gtk.Widget child)
``` 

Which is very self descriptive. `child` is the widget we want to append or prepend.

By the moment, we will use just the fill and expand properties, and will just pack the widgets in this order:

* Label
* Entry
* Button

And our code would be something like this:

```vala
box.append (label);
box.append (entry);
box.append (button);
```

Now, let's add our box to the window and see how it looks:

```vala
window.set_child (box);
```

Let's compile and run

```sh
valac main.vala -o my_first_app --pkg gtk4
```

It should work fine, but let's add some more information to the widgets. Let's add a label to the button

```vala
button.set_label ("Set text as window title");
```

Now, let's add the functionality that we need! Let's connect the clicked signal in the button. The property that contains the information in the GtkEntry is called `text`, and you can get it by using `entry.get_text ()`, method that will return a string. 

Try to solve it before seeing the answer!

<details>
We can solve it using:

```vala
button.clicked.connect (() => {
    var text = entry.get_text ();
    window.set_title (text);
});
```

Or we can make it shorter by using:

```vala
button.clicked.connect (() => {
    window.set_title (entry.get_text ());
});
```
</details>

Before we go, let's see a signal for entry!: `changed`

Changed is triggered when we add something to the entry, so let's see how it behaves by printing the value in entry into the terminal:

```vala
entry.changed.connect (() => {
    message (@"Content: $(entry.get_text ())");
});
```

As you can see, we are using parenthesis before putting the variable, as we are calling a function, we have ot put them in order to avoid unexpected issues.

## Spin Buttons

Spin buttons are a little bit different, they contain a range of numbers, and buttons to adjust them. This are often used in cases like selecting a number from a range (spin buttons contain a built in feature to mark a range within them). We will learn how to use them, and also, put them into a scale

We have two ways to create a spin button in GTK:

**By using an adjustment**:

```vala
var adjustment = new Gtk.Adjustment (1, -2, 8, 1, 3, 3);
var spin_button = new Gtk.SpinButton (adjustment, 2, 5);
```

As you can see, this is a little bit tricky, and adjustment is made by this values:

```vala
(double value, double lower, double upper, double step_increment, double page_increment, double page_size)
```

The first being the default value, the second the minimun value it can take, the third is the numbers from the minimum value that it will use, the step increment is how much it is going to grow when you press the button, and the last two the page increments and size. Fortunately, there's an easier way to actually do this if we are just going to need a single spinbutton:

```vala
var spin_button = new Gtk.SpinButton.with_range (-2, 5, 1);
```

As you can see this is way simpler, and it is composed by the following values:

```vala 
(double min, double max, double step)
```

Which are pretty self descriptive.

Now, let's pack it to the end of our box and run the program:

```sh
valac main.vala -o my_first_app --pkg gtk4
```

As you may have noticed, the buttons are blocked whenever the minimum or maximum values are reached, which is very practical. Now, we are going to use an scale (those range lines that you see when selecting a number in a app). Now, we will find the practicity of using an Adjustment instead. We will replace our current spin button, and use an adjustment instead, that we can use for the Scale too.

```
    var adjustment = new Gtk.Adjustment (1, -2, 8, 1, 3, 3);
    var spin_button = new Gtk.SpinButton (adjustment, 2, 5);
    var scale = new Gtk.Scale (Gtk.Orientation.HORIZONTAL, adjustment);
```

As you can see, we have used the same Adjustment for the scale, let's pack_start it to our box and run the program.

```vala
box.append (scale);
```

```sh
valac main.vala -o my_first_app --pkg gtk4
```

And, whoops, looks like magic! Now, I'll make you a challenge. Create a window with the following items:

* A label with the caption: "Write your name"
* An entry at its right

Below those two:
* A label with the caption: "Set your age"
* Below the label a spin button and a scale next to each other, they must be connected with an adjustment, which values are set from 0 to 117.

Below those, a single button with the caption:

* Done!
* When clicked, it will print the values with the following format:

* My name is $name and I'm $age

Before going, you can get the current value of a spinbutton by using the method: `get_value`. E.g

```vala
var my_value = my_spin_button.get_value ();
```

A little aclaration too: the last parameter for Gtk.Entry () is the number of decimals you will have

**Bonus** Make the button verify that the entry with the name is not empty!

Good luck! You'll find the answer right below here, but try it! You can use either boxes or grids, but think about how they will be distributed:

<details>

```vala
public static int main (string[] args) {
    var app = new Gtk.Application (
        "io.github.diegoivanme.gtk-tutorial",
        ApplicationFlags.FLAGS_NONE
    );

    app.activate.connect (() => {
        var window = new Gtk.ApplicationWindow (app);
        window.set_title ("My Data");

        var grid = new Gtk.Grid ();
        var label_name = new Gtk.Label ("Write your name: ");
        var entry_name = new Gtk.Entry ();

        var label_age = new Gtk.Label ("Set your age");
        var adjustment = new Gtk.Adjustment (0, 0, 117, 1, 5, 10);
        var spin_button = new Gtk.SpinButton (adjustment, 2, 0);
        var scale = new Gtk.Scale (Gtk.Orientation.HORIZONTAL, adjustment);

        // We can also create buttons with labels!
        var button = new Gtk.Button.with_label ("Done!");
        button.clicked.connect ( () => {
            var name = entry_name.get_text ();

            if (name == "") {
                critical ("Name entry is empty!");
                return;
            }

            var age = spin_button.get_value ();
            message (@"My name is $name and I'm $age");
        });

        grid.attach (label_name, 0, 0);
        grid.attach (entry_name, 1, 0);

        // Here were are using 2 width to use all the space available :P
        grid.attach (label_age, 0, 1, 2);
        grid.attach (spin_button, 0, 2);
        grid.attach (scale, 1, 2);
        grid.attach (button, 0, 3);

        window.set_child (grid);
        window.present ();
    });

    return app.run (args);
}
```

</details>
