# A Hello World in GTK4

Now, let's start to create a folder to store your first programs. Also, create a subfolder to add your first examples and excersises. I recommend naming it "01" and the file in it "main.vala"

As we always do when we learn something new, we will create our first hello world application. For this, we will need to start with a main function in Vala, as the following:

```vala
public static int main () {
        
}
```

Now, we will proceed with the creation of our window instance. We will create a `GtkApplicationWindow' with the following piece of code:

```vala
public static int main () {
   var window = new Gtk.ApplicationWindow ();
}
```

As you can see, Vala uses the `var` keyword to declare variables, but you can also use `Gtk.ApplicationWindow` instead of `var`. For learning purposes, we will leave it on `var`.

Now, what we have to do is set a title for this window!, so we will call the window method `set_title ()`

```vala
public static int main () {
    var window = new Gtk.ApplicationWindow ();
    window.set_title ("Hello World!");
}
```

Now, let's add something in it. What about a Label? Let's create one

```vala
public static int main () {
    var window = new Gtk.ApplicationWindow ();
    window.set_title ("Hello World!");
    
    var label = new Gtk.Label ("This is my first Gtk window");
}
```

It is not on the window yet, so let's add it to the main window.

```vala
public static int main () {
    var window = new Gtk.ApplicationWindow ();
    window.set_title ("Hello World!");
    
    var label = new Gtk.Label ("This is my first Gtk window");
    
	window.set_child (label);
}
```

Now, we can actually compile this one, but it won't show anything, because we are missing to important parts. Starting the **main loop** and presenting the window.

Now, you may be wondering what is the so called **main loop**. A very quick explanation: Gtk is event oriented, so when a widget modifies a value of other, it has to be updated to be shown in screen. Now, how can we do this? Well, running a loop that updates what is changed in the Widgets, this also allow us to have signalling, but we will be over it later. You can actually run a main loop without anything in it in Vala, just create a new file and type:

```vala
public static void main () {
    var loop = new MainLoop ();
    loop.run ();
}
```

This will clearly do nothing, just a loop will be infinintly running until you kill the process in your terminal. Oh, and if you want to actually test this one, you can compile things with:

```sh
valac filename.vala -o output_name
```

In which:
    
* *filename* is the name of the vala code file
* And *output_name* is the name of the binary that you can run in your machine
    
Now, you should be able to run your output program with

```sh
./output_name
```

Now, we can use a class named `GtkApplication` in order to start the main loop. This one also has other utilities including finishing the app when the window is closed, handling accelerators (that we will see in future chapters) and some more stuff.

Let's create it using its constructor, which takes two parameters:

```vala
Gtk.Application (string? application_id, Gtk.ApplicationFlags flags)
```

Let's explain each one of them:

* `application_id`: Is the unique identifier of our application. A reverse domain is frequently used for this. For example, if our domain is `example.org` and our app name is `myApp`, our application id would be `org.example.myApp`. Some other examples over here:
	* `org.gnome.TextEditor` for gnome.org and TextEditor
	* `com.github.bleakgrey.tootle` for github.com and Tootle
	* `im.riot.Riot` and riot.im for Riot (Element)
* `flags`: Is the role our application meets in the system. For example, if it's a service or handles command line arguments, but in our case, we will use `ApplicationFlags.FLAGS_NONE` as we don't need any special treatment for our use case.

Now, let's create our application according to this parameters. In my case, I'll use a reverse Github domain, which will be `io.github.diegoivanme.gtk-tutorial`, you can use this, but I encourage you to create one for your very specific use case.

```vala
var app = new Gtk.Application ("io.github.diegoivanme.gtk-tutorial", ApplicationFlags.FLAGS_NONE);
```

As you can see, this line of code is a little bit big. We can separate parameters into different lines in order to make it look a little bit better, just like this:

```vala
var app = new Gtk.Application (
	"io.github.diegoivanme.gtk-tutorial",
	ApplicationFlags.FLAGS_NONE
);
```

For this GtkApplication, we should do something when it is activated. This is the first signal we will manage, named `activate`. In order to connect signals, we have to write something like this:

```vala
object_instance.signal_name.connect
```

Bringing it too our Application instance:

```vala
app.activate.connect
```

Now, we should create an arrow function in order to execute for it. If you do not know what an arrow function, do not worry, we will see step by step how to create one:

First, let's open parenthesis next to `connect`:

```vala
app.activate.connect ();
```

The next step is opening another set of parenthesis in those, this will be the parameters we will parse to the arrow function, in this case, none.

```vala
app.activate.connect (() );
```

Now, we will write an equal sign next to a greater than sign, just like this: `=>` creating something like an arrow. We will write this after the parameters

```vala
app.activate.connect (() => );
```

This arrow is pointing somewhere, and this is the function. We will open curly brackets and write the code between those, something like this:

```vala
app.activate.connect (() => { message ("Hello World"); });
```

Or we can make it prettier:

```vala
app.activate.connect (() => {
	message ("Hello World");
});
```

So, now that we know how to create arrow functions, we will 	create our window for our application in there.

We will move our window creation instance into that function, as well as the label creation, `set_title`, `set_child` and `present`, in there, it will look something like this:

```vala
app.activate.connect (() => {
	message ("Hello World");
	var window = new Gtk.ApplicationWindow (app);
	window.set_title ("Hello World!");
        
	var label = new Gtk.Label ("This is my first Gtk window");

	window.set_child (label);
	window.present ();
});
```

Now, pretty much everything is done. We just have to run the app! But we have some stuff missing. First, our main function is not parsed any arguments. In this case, we just have to add them like this:

```vala
public static int main (string[] args) {
```

If you are wondering what `args` are, they are just basically the things we put on terminal when the binary is executed, just like you parse to `flatpak` the `install` and `app_id`. In this case, our app does not handle them, so we are fine not doing too much with them.

Then, as return value, we will write what running the app returns and we will parse the args. Our main function return statement will look something like this:

```vala
return app.run (args);
```

And our whole file will look like this:

```vala
public static int main (string[] args) {
    var app = new Gtk.Application (
        "io.github.diegoivanme.gtk-tutorial",
        ApplicationFlags.FLAGS_NONE
    );

    app.activate.connect (() => {
        message ("Hello World");
        var window = new Gtk.ApplicationWindow (app);
        window.set_title ("Hello World!");

        var label = new Gtk.Label ("This is my first Gtk window");

        window.set_child (label);
        window.present ();
    });

    return app.run (args);
}
```

Now, let's compile and run, with the following command in Terminal:

```sh
valac main.vala -o my_first_app --pkg gtk4
```

For running, type:

```sh
./my_first_app
```

And... Tada! You have created your first Gtk window, as you can see, it starts quite small and it does not look like those cool GNOME apps, but don't worry, we are just starting :D.
