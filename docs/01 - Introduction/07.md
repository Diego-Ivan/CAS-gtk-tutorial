# Stacks and StackSwitchers

Have you ever seen those shiny apps with different pages and a bar on the top that allows you to *switch* from different views?. Well those bars are named GtkStackSwitchers, and the different views are contained on a GtkStack.

Let's clean up our file to leave it just with the headerbar and the window:

```vala
public static int main (string[] args) {
    var app = new Gtk.Application (
        "io.github.diegoivanme.gtk-tutorial",
        ApplicationFlags.FLAGS_NONE
    );

    app.activate.connect (() => {
        var window = new Gtk.ApplicationWindow (app);
        var header_bar = new Gtk.HeaderBar ();
        window.set_title ("My App");

        window.set_titlebar (header_bar);
        window.set_default_size (600, 400);

        window.present ();
    });

    return app.run (args);
}
```

Now, we have to create a stack to put it in the window content, so let's learn how to create a stack:

```vala
var stack = new Gtk.Stack ();
```

As you can see, it does not take any parameters for construction, then, let's see how can we add some pages to it. There are two method:

```vala
stack.add_named (GtkWidget child, string name)
```

And:

```vala
stack.add_titled (GtkWidget child, string name, string title)
```

And you may be wondering, what's the difference? Well, ´name´ and ´title´ work similarly but not the same. Remember the "string representation" of GObjects? Well, this is kinda similar, you add a name to page and then you can get it. But, the ´title´ is what you see on the top of the apps, in the Switcher to be more clear. I hightly recommend using the second method if you are going to use a stack switcher, but for use cases outside of it, maybe the first one is the way to go.

Well, let's create a label and an image for two pages in the stack switcher.

```vala
var label = new Gtk.Label ("My Stack!");
var image = new Gtk.Image.from_icon_name ("mail-send-symbolic");
```

Now, that we have them, let's add them to the stack. Let's start with the label

```vala
stack.add_titled (label, "label-page", "Page 1");
```

I think that with this you might be able to understand what's going on here, and the difference between name and title is probably more clear. "label-page" is a property name that we will use to refer to it, and "Page 1" is the displayed title. But we'll see that later ;)

Let's add the icon:

```vala
stack.add_titled (image, "image-page", "Page 2");
```

And, now, let's add the stack to the window!

```vala
window.set_child (stack);
```

Let's compile and run!

```sh
valac main.vala -o my_first_app --pkg gtk4
```

And... Well, the first page is shown, which is cool but... how do we change to the second? Let's move on to the stackswitcher! But first, lets learn how to create one.

```vala
var stack_switcher = new Gtk.StackSwitcher ();
```

As you can see, it takes no other parameter, then, let's assign it a stack!

```vala
stack_switcher.set_stack (stack);
```

Pretty self descriptive, isn't it? It takes as only parameter a `GtkStack`.

But... how do we append it to the HeaderBar? Well, if you remember, you can assign a GtkWindow *any* GtkWidget as titlebar, but it doesn't mean you should... Well, try it. I'll let you see what happens. Spoiler alert, it's broken.

This is kinda dissapointing, but HeaderBars can have Custom Widget Titles!!, this means you can add *any* GtkWidget to a HeaderBar without breaking the close buttons, the titlebar itself, or any other widget you want to append to it (as many applications do).

But, how? Well, using this very cool method:

```vala
set_title_widget (Gtk.Widget title_widget)
```

Now we are talking, let's implement it to our window! Let's replace the:

```vala
window.set_title ("My App");
```

With:

```vala
header_bar.set_title_widget (stack_switcher);
```

Now, let's compile and run!

```sh
valac main.vala -o my_first_app --pkg gtk4
```

And voilá! You can now switch between pages! But, as you can see, it doesn't look that flashy as other apps do. This one is more like an ugly slide presentation, has no transitions, no personality. Let's add it some, then! Let's start with the most common: a nice crossfade

```vala
stack.set_transition_type (Gtk.StackTransitionType.CROSSFADE);
```

This one takes as parameter an enumerator containing the different transitions

Compile and run!

If you think the transition is *slow* or very <u>fast</u>, then, let's change the transition duration in miliseconds with:

```vala
set_transition_duration (uint duration)
```

As you can see, it takes as parameter the duration in miliseconds as an unsigned integer (an integer that doesn't take negative values). The default is 200 seconds, but you can experiment! With a second-long duration, for example:

```vala
stack.set_transition_duration (1000);
```

Compile and run!

Now, if you are interested in transitons, I'll leave the list with the enumerator over here, play around with them!:

* CROSSFADE - A cross-fade
* NONE - No transition (boring)
* OVER_DOWN - Cover the old page by sliding down.
* OVER_DOWN_UP - Cover the old page sliding down or uncover the new page sliding up, according to order. (recommended for vertical stackswitchers)
* OVER_LEFT - Cover the old page by sliding to the left.
* OVER_LEFT_RIGHT - Cover the old page sliding left or uncover the new page sliding right, according to order. (Recommended for horizontal stackswitchers)
* OVER_RIGHT - Cover the old page by sliding to the right.
* OVER_RIGHT_LEFT - Cover the old page sliding right or uncover the new page sliding left, according to order. (Recommended for horizontal stackswitchers)
* OVER_UP - Cover the old page by sliding up.
* OVER_UP_DOWN - Cover the old page sliding up or uncover the new page sliding down, according to order. (recommended for vertical stackswitchers)
* SLIDE_DOWN - Slide from top down
* SLIDE_LEFT - Slide from right to left
* SLIDE_LEFT_RIGHT - Slide from left or right according to the children order (Recommended for horizontal stackswitchers)
* SLIDE_RIGHT - Slide from left to right
* SLIDE_UP - Slide from bottom up
* SLIDE_UP_DOWN - Slide from top down or bottom up according to the order (recommended for vertical stackswitchers)
* UNDER_DOWN - Uncover the new page by sliding down.
* UNDER_LEFT - Uncover the new page by sliding to the left.
* UNDER_RIGHT - Uncover the new page by sliding to the right.
* UNDER_UP - Uncover the new page by sliding up.

(Enumerator documentation taken from: (Valadoc)[https://valadoc.org/gtk4/Gtk.StackTransitionType.html]
