# Managing signals and callbacks

Now, we are going to add some more functionalities to our button. Currently, it is just in the grid and it cannot do anything, it can just be pressed but no response to it can be made. We are going to add some to it. First, let's see what makes a button respond to our command. 

Every time we click a button, a signal is triggered, and this one is called ´clicked´, very self descriptive, right? And we can actually call it in our program by adding ´button.clicked ()´, as this is the trigger for the signal. But now, let's try to do something more. Let's *connect* the signal to a certain event we want to be triggered. To do this we will add the following lines of code:

```vala
button.clicked.connect ( () => {
    message ("Hello World");
});
```

We add the `connect` keyword to relate the signal with a callback or response to it.

If you are not a fan of arrow functions, you can create a separated method and then put in the parenthesis:

```vala
button.clicked.connect (my_callback)

...

public void my_callback () {
    message ("Hello world");
}
```

This would do the exact same thing. By the way, you may have noticed that ´my_callback´ on the connect statement **does not** have the usual parenthesis that we would add when calling a method. This is because we do not need them, and because callbacks *just work* in that way, so don't worry if you are a little bit confused with that, you'll get used to it.

Now, let's compile and run!

```sh
valac main.vala -o my_first_app --pkg gtk4
```

And, well, when clicked, the button should be printing something like:

```
** Message: 18:16:10.360: main.vala:21: Hello World
```

Now that we know how to use signals, let's create a button that counts how many times you've pressed it!

First, we will declarate an integer as our counter:

```vala
int times_pressed = 0;
```

Now, we will change the label that we added to our button to:

```vala
button.set_label ("You have pressed me 0 times!");
```

Now, we must create a way to put it on screen, right? So let's change the callback

```vala
button.clicked.connect (() => {
    times_pressed++;
    button.set_label ("You have pressed me: " + times_pressed.to_string () + " times!");
});
``` 

If you compile it an run it, it should work just fine... *but*, we can make it **cooler**. As you can see, we have used string concatenation and the ´to_string ()´ method that the numeric types have, but Vala has its own cool feature to put variables inside a string without having to concatenate it. If you have been into shell scripting, you may be familizarized with the ´$VARIABLE_NAME´ syntax, and, indeed, it's very similar.

We use the `$variable_name` to put a value stored in a variable in a string. Now, the only missing thing is adding a `@` before the commas, leaving us something like this:

```vala
@"Cool Stuff $variable"
```

Now, we can add it to our ´set_label´ method (by the way, you may have noticied the word 'method' is frequently used in this tutorial, instead of the word function. In Vala, by convention, we use the word 'method' even if the function is not inside an object)

```vala
button.set_label (@"You have pressed me: $times_pressed times!");
```

Now, you should be able to compile it and run it without any issues. We have now created a button that counts how many times you've pressed it, we learnt how to use callbacks and some other cool Vala language features.
